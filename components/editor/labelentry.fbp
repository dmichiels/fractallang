% Logic stream
in => in awIn(QTk/actionsWrapper)
awIn() out -> in awOut(QTk/actionsWrapper) out => out
awOut() action => action

% UI stream
awIn() action#create -> in create(editor/labelentry/create) out -> in label(QTk/label)
create() out -> in entry(QTk/entry)
entry() out -> place ph(QTk/placeholder)
label() out -> place ph() out -> in awOut()
"create(bg:white)" -> in ph()

% ---
% Double click on label switch to the entry
label() action#ButtonPress -> input dclick(mouse/doubleclick)
dclick() output -> input ieLabel(ipEdit) out -> in entry()
"opt(text:display)" -> options ieLabel()

% ---
% Focus out, enter switch to the label with the new text
entry() action#KeyPress -> input qEntry(editor/labelentry/quitEntry)
entry() action#FocusOut -> input qEntry()
qEntry() change -> in entry() action#getText -> input setText(editor/labelentry/setText)
setText() output -> in label()
qEntry() esc -> in label()
setText() textchange -> in awOut()

% ---
% Remove click for upper level
awOut() action#ButtonPress -> input d(discard)

% ---
% set from outside
awIn() action#set -> in label()
awIn() action#set -> in entry()

% hack : have to change display in entryDisplay/labelDisplay
% Used in selectPort
awIn() action#display -> in entry()
awIn() action#getFocus -> in entry()

% ---
% delete
awIn() action#delete -> in label()
awIn() action#delete -> in entry()
awIn() action#delete -> in ph()