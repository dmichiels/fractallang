% ---
% Logic stream
in => in awIn(QTk/actionsWrapper)
awIn() out -> in awOut(QTk/actionsWrapper) out => out
awOut() action => action

% ---
% UI stream
awIn() action#create -> in create(editor/component/createPort) rect -> in rect(QTk/canvas/rectangle)
rect() out -> in awOut()
create() name ->  in text(QTk/canvas/text) out -> in awOut()

% ---
% Move
awIn() action#move -> in rect()
awIn() action#move -> input moveLogic(editor/component/moveLogic) output -> in awOut()
awIn() action#move -> in text()

% ---
% setCoords
awIn() action#setCoords -> input center(editor/component/center) rect -> in rect()
center() text -> in text()

% ---
% Hide and show
awIn() action#set -> in rect()
awIn() action#set -> in text()
rect() action#Enter -> in awOut()
text() action#Enter -> in awOut()

% ---
% delete
awIn() action#delete -> in rect()
awIn() action#delete -> in text()
awIn() action#delete -> in awOut()

% ---
% Don't send the moveBegin and moveEnd action outside
awOut() action#moveBegin -> input discard(discard)
awOut() action#moveEnd -> input discard()


% ---
% Leftclick : createLink out
%             so getCoord, get EP of awIn and send create
% getCoord
rect() action#ButtonRelease -> input ieCoord(ipEdit) out -> in rect()
rect() action#getCoordLC -> coord lc(editor/component/leftClick)
"opt(text:getCoords(output:getCoordLC))" -> options ieCoord()
% getEntryPoint
rect() action#ButtonRelease -> input ieEP(ipEdit) out -> in awOut()
awOut() action#getEntryPointLC -> ep lc()
"opt(text:getEntryPoint(output:getEntryPointLC))" -> options ieEP()
% getName
rect() action#ButtonRelease -> input ieName(ipEdit) out -> in text()
text() action#getNameLC -> name lc()
"opt(text:get(text output:getNameLC))" -> options ieName()
% main stream
rect() action#ButtonRelease -> input lc() output -> in awOut()
% Remove ButtonPress for upper level
rect() action#ButtonPress -> input discard(discard)
% Idem for text
text() action#ButtonRelease -> input ieCoord()
text() action#ButtonRelease -> input ieEP()
text() action#ButtonRelease -> input lc()
text() action#ButtonRelease -> input ieName()